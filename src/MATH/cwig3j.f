      double precision function cwig3j (j1,j2,j3,m1,m2,ient)
c     wigner 3j coefficient for integers  (ient=1)
c                         or semiintegers (ient=2)
c     other arguments should be multiplied by ient

      implicit double precision (a-h,o-z)
      parameter (idim = 58)
      character*512 slog
c     dimensions  modified for larger arguments by ala 12.12.94
      dimension al(idim+1),m(12)
      save ini, al
      data ini/1/
c     idim-1 is the largest argument of factorial to calculate

      m3=-m1-m2
      if (ini.eq.0) goto 21
c        initialisation of the log's of the factorials
      ini=0
      al(1)=0.0d00
      do i=1,idim
         b=i
         al(i+1)=al(i)+ log(b)
      enddo
 21   continue
      cwig3j=0.0d00
      if (((ient-1)*(ient-2)).ne.0) go to 101
      ii=ient+ient
c        test triangular inequalities, parity and maximum values of m
      if (( abs(m1)+ abs(m2)).eq.0.and.mod(j1+j2+j3,ii).ne.0) go to 99
      m(1)=j1+j2-j3
      m(2)=j2+j3-j1
      m(3)=j3+j1-j2
      m(4)=j1+m1
      m(5)=j1-m1
      m(6)=j2+m2
      m(7)=j2-m2
      m(8)=j3+m3
      m(9)=j3-m3
      m(10)=j1+j2+j3+ient
      m(11)=j2-j3-m1
      m(12)=j1-j3+m2
      do i=1,12
         if (i.gt.10) go to 31
         if (m(i).lt.0) go to 99
 31      if (mod(m(i),ient).ne.0) go to 101
         m(i)=m(i)/ient
         if (m(i).gt.idim) go to 101
      enddo

c        calculate 3j coefficient
      max0= max(m(11),m(12),0)+1
      min0= min(m(1),m(5),m(6))+1
      isig=1
      if (mod(max0-1,2).ne.0) isig=-isig
      c=-al(m(10)+1)
      do i=1,9
         c=c+al(m(i)+1)
      enddo
      c=c/2.0d 00
      do i=max0,min0
         j=2-i
         b=al(i)+al(j+m(1))+al(j+m(5))+
     $        al(j+m(6))+al(i-m(11))+al(i-m(12))
         cwig3j=cwig3j+isig* exp(c-b)
         isig=-isig
      enddo
      if (mod(j1-j2-m3,ii).ne.0) cwig3j=-cwig3j
 99   return
 101  write(slog,'(a,6i5)') 'error in cwig3j ',j1,j2,j3,m1,m2,ient
      call wlog(slog)
      stop
      end
